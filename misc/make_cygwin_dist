#!/bin/tcsh -f

if(! -f configure) then
  echo "run this script from the root directory of CVS working directory"
  echo "which is where the 'conigure' script resides"
  exit 1
endif

if( "$1" == "") then
  echo "usage : $0 <version> <win-flavor> <name extension> <options to configure ...>"
exit
endif

set version=$1
set winflavor=$2
set darflavor=$3

set dir_name="dar$darflavor-$version-$winflavor"
set build_dir=`pwd`/$dir_name
echo "install directory is $build_dir"
if( -e $build_dir) then
   echo "cannot create $build_dir file exists"
   exit 1
endif
mkdir "$build_dir"
make clean distclean || echo "ignoring error, tree already clean"
# setenv CXXFLAGS -O
./configure --prefix=/ --bindir=/ --libdir=/ --datarootdir=/share --sysconfdir=/$dir_name/etc --disable-gpgme-linking $4 $5 $6 $7 $8 $9 ${10} ${11}
make $MAKE_OPT
make DESTDIR="$build_dir" install-strip
cp /bin/cygwin1.dll /bin/cygz.dll /bin/cygbz2-1.dll /bin/cygiconv-2.dll /usr/bin/cygintl-8.dll /bin/cyggcc_s-seh-1.dll /bin/cygstdc++-6.dll /bin/cyggpg-error-0.dll /bin/cyglzo2-2.dll /usr/bin/cyggcrypt-20.dll /usr/bin/cyggpgme-11.dll /usr/bin/cyglzma-5.dll /bin/cygrsync-2.dll /bin/cygcurl-4.dll /bin/cygcrypto-1.0.0.dll /bin/cyggssapi_krb5-2.dll /bin/cygidn2-0.dll /bin/cyglber-2-5-0.dll /bin/cygldap-2-5-0.dll /bin/cygnghttp2-14.dll /bin/cygpsl-5.dll /bin/cygssh2-1.dll /bin/cygssl-1.0.0.dll /bin/cygunistring-2.dll /bin/cygk5crypto-3.dll /bin/cygkrb5-3.dll /bin/cygkrb5support-0.dll /bin/cygcom_err-2.dll /bin/cygsasl2-3.dll /usr/bin/cygbrotlidec-1.dll /usr/bin/cygcrypto-1.1.dll /usr/bin/cygssh-4.dll /usr/bin/cygssl-1.1.dll /usr/bin/cygbrotlicommon-1.dll $build_dir
mv "$build_dir/share/dar" "$build_dir/doc"
mv "$build_dir/$dir_name/etc" "$build_dir/etc"
rm -rf "$build_dir/share" "$build_dir/include" "$build_dir/pkgconfig" "$build_dir/python3" "$build_dir/$dir_name"

foreach fichier (README TODO INSTALL ChangeLog THANKS COPYING)
   cp $fichier "$build_dir/`basename $fichier`.txt"
   misc/todos "$build_dir/`basename $fichier`.txt"
end

zip -9 -r "$dir_name".zip $dir_name
rm -rf $build_dir
